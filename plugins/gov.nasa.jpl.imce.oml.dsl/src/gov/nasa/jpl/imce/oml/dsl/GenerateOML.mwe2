/*
 * Copyright 2017 California Institute of Technology ("Caltech").
 * U.S. Government sponsorship acknowledged.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 * License Terms
 */
 module gov.nasa.jpl.imce.oml.dsl.GenerateOML

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var baseName = "gov.nasa.jpl.imce.oml.dsl"
var modelName = "gov.nasa.jpl.imce.oml.model"

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = baseName
				rootPath = rootPath
				runtimeTest = {
					enabled = true
					root = "../../tests/${baseName}.tests"
				}
				eclipsePlugin = {
					enabled = true
					root = "../gov.nasa.jpl.imce.oml.dsl.ui"
				}
				eclipsePluginTest = {
					enabled = true
					root = "../../tests/${baseName}.ui.tests"
				}
				createEclipseMetaData = true
				genericIde = {
					root = "../gov.nasa.jpl.imce.oml.dsl.ide"
				}
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * Copyright 2017 California Institute of Technology (\\\"Caltech\\\").\n * U.S. Government sponsorship acknowledged.\n *\n * Licensed under the Apache License, Version 2.0 (the \\\"License\\\");\n * you may not use this file except in compliance with the License.\n * You may obtain a copy of the License at\n *\n *     http://www.apache.org/licenses/LICENSE-2.0\n *\n * Unless required by applicable law or agreed to in writing, software\n * distributed under the License is distributed on an \\\"AS IS\\\" BASIS,\n * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n * See the License for the specific language governing permissions and\n * limitations under the License.\n */"
			}
		}
		language = StandardLanguage {
			name = "${baseName}.OML"
			fileExtensions = "oml"
			
			referencedResource = "platform:/resource/org.eclipse.emf.ecore.xcore.lib/model/XcoreLang.xcore"
			referencedResource = "platform:/resource/${modelName}/model/OMLCommon.xcore"
			referencedResource = "platform:/resource/${modelName}/model/OMLTerminologies.xcore"
			referencedResource = "platform:/resource/${modelName}/model/OMLGraphs.xcore"
			referencedResource = "platform:/resource/${modelName}/model/OMLBundles.xcore"
			referencedResource = "platform:/resource/${modelName}/model/OMLDescriptions.xcore"

			formatter = {
				generateStub = true
				generateXtendStub = true
			}

			serializer = {
				generateStub = false
			}
			validator = {
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}

			newProjectWizardForEclipse = {
				generate = true
			}
			generator = {
				generateStub = false
				generateXtendMain = false
				generateXtendStub = false
			}
		}
	}
}
